#!/usr/bin/env perl
use strict;
use warnings;
use FindBin '$RealBin';
use File::Basename;
use File::Spec;
use lib File::Spec->catfile(dirname($RealBin), 'lib');
use Verman;

sub sep {
  local $_ = $_ // shift;
  /\A[+;]\Z/
}

sub process_flags {
  while (@_ and $_[0] =~ s/^--//) {
    my ($var, $val) = split '=', shift, 2;
    $ENV{"VERMAN_\U$var\E"} = $val // 1
  }
}

sub parse_cmds {
  my @cmds;
  &process_flags;
  cmd: while (@_) {
    shift if sep $_[0];
    arg: for my $i (0..$#_) {
      next arg unless sep $_[$i];
      push @cmds, [splice @_, 0, $i];
      next cmd;
    }
    push @cmds, [splice @_];
  }
  @cmds
}

sub main {
  my $v = Verman->new;
  my @cmds = (exists $ENV{verman_old}) ? [@_] : parse_cmds(@_);
  @cmds = ([]) unless @cmds;
  $v->cmd($0, @$_) for @cmds;
}

main @ARGV;
